apply plugin: 'groovy'

ext {
    // these values must match the target installation
    jobDslVersion = '1.66'
    jenkinsVersion = '2.92'
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
    }
}

repositories {
    maven {
        url 'https://plugins.gradle.org/m2'
        url 'https://repo.jenkins-ci.org/public'
    }
    jcenter()
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

dependencies {
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'

    // Jenkins test harness dependencies
    testCompile('org.jenkins-ci.main:jenkins-test-harness:2.32') {
        exclude(module: 'org-netbeans-insane')
    }
    testCompile "org.jenkins-ci.main:jenkins-war:2.92"
//    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.10@jar'

    // plugins to install in test instance

    // search here --> https://repo.jenkins-ci.org/webapp/#/home
    // groupId:artifactId:version
    testPlugins 'org.jenkins-ci.plugins:git:3.6.4'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.3'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.39.0'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.28.1'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}
